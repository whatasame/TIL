# 2) 아두이노와 시리얼 통신

## 아두이노 소개

아두이노를 활용하면 다양한 하드웨어 부품을 쉽게 제어할 수 있습니다.

<p align="center"><img src="../../images/마이크로프로세서/2) 아두이노와 시리얼 통신-Untitled.png"></p>

아두이노는 ATmega328라는 8bit 마이크로컨트롤러를 가지고 있습니다. 아두이노는 ATmega328을 통해 다양한 처리를 함과 동시에 하드웨어를 제어할 수 있습니다.

- RX : Receive
- TX : Transfer
- 정전압 레귤레이터 : Input Voltage 7~12V를 IC에 적합한 5V로 변환하는 부품
- 3.3V : 통신 모듈용으로 사용
- Crystal oscillator : 16Mhz의 Clock을 만드는 부품
- 14개의 디지털 I/O 핀
    - 0번, 1번 (2개) : Serial I/O Pin
    - 2번~13번 (12개) : Digital I/O Pin (이 중 6개는 PWM Pin)
- 전원 공급 단자 3개
- 기본 입출력 단자 :
    - 입력 → Reset 버튼
    - 출력 → 13번 pin LED

## 전압, 전류, 저항

### 전압

- 전위가 높은 쪽과 낮은 쪽의 차이
- 1쿨롱의 전하 갖고있는 에너지
- Ardunio에서는 직류 3.3V, 5V를 지원

### 전류

- 1초당 1쿨롱의 전하가 단위 면적을 통과했을 때 1A로 정의
- Arduino에서는 1mA 단위를 사용

### 저항

- 전류의 흐름을 방해하는 정도를 나타냄
- 색 띠나 숫자로 값을 표시
- Arduino에서는 칩 형태의 저항을 사용

## Serial 통신

<p align="center"><img src="../../images/마이크로프로세서/2) 아두이노와 시리얼 통신-Untitled 1.png"></p>

데이터 통신 방법에는 Serial 통신과 Parallel 통신이 있습니다. 데이터를 한 번에 하나씩 보내는 Serial 통신과 데이터를 한 번에 여러개 보내는 병렬 통신인 Parallel이 있습니다. 후자의 경우 Seiral 통신에 비해 빠르지만 복잡하고 비싸기 때문에 Arduino에서는 Serial 통신을 사용합니다.

아두이노에서 Serial 통신을 사용하는 목적은 아래와 같습니다.

- 데이터 통신 : Arduino와 컴퓨터 혹은 다른 장치와 통신
- 디버깅

## Serial 통신 사용법

### 기본 명령어

- Serial.begin(Transmission rate)
    
    Serial 통신 속도를 정합니다. 단위는 bps로서 Arduino에 적합한 통신 속도는 9600bps입니다.
    
    <aside>
    💡 bps(bit per second), Bps(byte per second) → 1Bps = 8bps
    
    </aside>
    
- delay(ms)
    
    ms단위로 대기합니다.
    
- pinMode(Pin_number, setting)
    
    해당 핀 번호의 입출력 모드를 설정합니다. INPUT을 입력하면 해당 핀 번호는 입력 모드가 되고, OUTPUT을 입력하면 출력 모드가 됩니다.
    
- digitalWrite(Pin_number, value)
    
    해당 핀에 디지털 출력(High or Low)를 합니다.
    
- isDigit()
    
    변수의 값이 ASCII 코드에서 0~9의 범위에 있는지 판단합니다. 범위에 속할 경우 True를 반환합니다.
    

### 시리얼 모니터 출력 (Arduino → 시리얼 모니터)

- Serial.print(value)
    
    시리얼 모니터에 내용을 출력합니다.
    
- Serial.print(value, type)
    
    시리얼 모니터에 값을 출력하되 2진수(BIN), 10진수(DEC), 16진수(HEX)의 형태로 출력합니다.
    
- Serial.println(value)
    
    Serial.print와 동일한 내용을 출력하지만 마지막에 줄바꿈을 합니다.
    
- Serial.write(char value)
    
    시리얼 모니터에 아스키 코드 값을 보냅니다. e.g. write(65) → A 출력
    
    <aside>
    💡 print와 write의 차이 : print(문자→아스키코드→문자), write(문자→아스키코드)
    
    </aside>
    

### 시리얼 모니터 쓰기 (시리얼 모니터 → Arduino)

- Serial.available()
    
    시리얼 통신에 수신된 데이터가 있는지 확인합니다. 수신된 데이터가 있을 경우 True를 반환합니다.
    
- Serial.read()
    
    시리얼 통신을 통하여 수신된 값을 ASCII 코드로 반환합니다.
    
    e.g. Serial.read()로 읽은 값이 A일 경우 65로 반환함
    

### 예제

1. Serial 통신을 활용하여 Arduino에서 컴퓨터로 1초마다 증가하는 시간을 출력하라.
    
    <p align="center"><img src="../../images/마이크로프로세서/2) 아두이노와 시리얼 통신-Untitled 2.png"></p>
    
    ```arduino
    int number = 0; // -32768~32767 범위의 변수 number 설정, 초기값은 0
    void setup()
    {
        Serial.begin(9600); // 9600bps로 시리얼 통신 설정
    }
    void loop()
    {
        Serial.print(number);   // number 변수값 출력
        Serial.println(" sec"); // " sec"를 출력 후 줄 바꿈
        delay(1000);            // 1초동안 지연시킨다.
        number++;               // number 변수값을 하나 증가시킨다.
    }
    ```
    

1. char로 선언된 변수, int로 선언된 변수, float로 선언된 변수를 print와 write로 출력하라
    
    <p align="center"><img src="../../images/마이크로프로세서/2) 아두이노와 시리얼 통신-Untitled 3.png"></p>
    
    ```arduino
    // 65란 숫자를 유형별 변수에 저장한다.
    char charValue = 65;
    int intValue = 65;
    float floatValue = 65.0;
    // 문자열 네가지를 설정한다.
    String stringValue[] = {"Binary:", "Decimal:", "Hexadecimal:", "ASCII:"};
    void setup()
    { // 9600bps로 시리얼 통신 설정
        Serial.begin(9600);
    }
    void loop()
    {
        // 'char Value'를 출력하고 문자열과 숫자를 변수 유형별로 출력한다.
        Serial.println("*** char Value ***");
        Serial.print(stringValue[0]);   // stringValue 중 첫 번째 문자열 출력
        Serial.println(charValue, BIN); // 2진수 형태로 출력
        Serial.print(stringValue[1]);   // stringValue 중 두 번째 문자열 출력
        Serial.println(charValue, DEC); // 10진수 형태로 출력
        Serial.print(stringValue[2]);   // stringValue 중 세 번째 문자열 출력
        Serial.println(charValue, HEX); // 16진수 형태로 출력
        Serial.print(stringValue[3]);   // stringValue 중 네 번째 문자열 출력
        Serial.write(charValue);        // charValue에 해댕하는 ASCII 코드값 출력
        // 줄바꿈
        Serial.println();
        // 'int Value'를 출력하고 문자열과 숫자를 변수 유형별로 출력한다.
        Serial.println("*** int Value ***");
        Serial.print(stringValue[0]);
        Serial.println(intValue, BIN);
        Serial.print(stringValue[1]);
        Serial.println(intValue, DEC);
        Serial.print(stringValue[2]);
        Serial.println(intValue, HEX);
        Serial.print(stringValue[3]);
        Serial.write(intValue);
        Serial.println();
        Serial.println();
        // 'float Value'를 출력하고 문자열과 숫자를 변수 유형별로 출력한다.
        Serial.println("*** float Value ***");
        Serial.print("float Value:");
        Serial.println(floatValue);
        Serial.println();
        charValue++;       // charValue 1 증가
        intValue++;        // intValue 1 증가
        floatValue += 0.1; // floatValue 0.1 증가
        delay(1000);       // 1초동안 지연시킨다.
    }
    ```
    

1. Arduino에 입력된 숫자만큼 LED를 점멸하라
    
    ```arduino
    const int ledPin = 13; // LED 출력을 할 핀 번호 설정
    int blinkNumber = 0;   // 점멸횟수 변수 설정
    void setup()
    {
        Serial.begin(9600);      // 9600bps로 시리얼 통신 설정
        pinMode(ledPin, OUTPUT); // 13번 핀을 출력으로 설정
    }
    void loop()
    {
        // 시리얼 통신으로 입력 받은 데이터가 있는지를 검사하여 데이터가 있을 경우에 if문 안의 명령어를 실행
        if (Serial.available())
        {
            char val = Serial.read(); // val 변수에 시리얼 통신값 읽어오기
            if (isDigit(val))
            { // 입력된 값이 0~9의 숫자인지를 판단
                // val은 ASCII 코드값이므로 숫자로 바꿔주기 위하여 '0'의 아스키 코드값을 빼줌 blinkNumber에는 실제 숫자가 저장된다.
                blinkNumber = (val - '0');
            }
        }
        // blinkNumber 만큼 LED를 점멸시킴
        for (char i = 0; i < blinkNumber; i++)
        {
            digitalWrite(ledPin, HIGH);
            delay(200);
            digitalWrite(ledPin, LOW);
            delay(200);
        }
        blinkNumber = 0; // 점멸 횟수를 리셋함
    }
    ```